PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX xsd: <http://www.w3.org/2001/XMLSchema#>
PREFIX sn: <http://www.ldbc.eu/ldbc_socialnet/1.0/data/>
PREFIX snvoc: <http://www.ldbc.eu/ldbc_socialnet/1.0/vocabulary/>
PREFIX sntag: <http://www.ldbc.eu/ldbc_socialnet/1.0/tag/>
PREFIX foaf: <http://xmlns.com/foaf/0.1/>
PREFIX dbpedia: <http://dbpedia.org/resource/>
PREFIX dbpedia-owl: <http://dbpedia.org/ontology/>
 
SELECT ?messageYear ?isComment ?lengthCategory ?messageCount ?averageMessageLength ?sumMessageLength ?totalMessageCount ?percentageOfMessages
WHERE {
  {
    SELECT ?messageYear ?isComment ?lengthCategory (COUNT(?message) AS ?messageCount) (AVG(?length) as ?averageMessageLength) (SUM(?length) as ?sumMessageLength) 
    WHERE {
      
      {
        ?message rdf:type snvoc:Comment .
        BIND( true as ?isComment)
      } UNION {
        ?message rdf:type snvoc:Post .
        BIND( false as ?isComment)
      } .
      ?message snvoc:length ?length .
      ?message snvoc:creationDate ?creationDate .
      
      FILTER (?creationDate < "2017-07-15T11:50:56") .
      
      BIND( year(?creationDate) as ?messageYear) .
      
      BIND( IF (?length < 40, 0, IF (?length < 80 , 1, IF(?length < 160, 2, 3))) as ?lengthCategory)
       
    }
    GROUP BY ?messageYear ?isComment ?lengthCategory
  }
   
  {
    SELECT (COUNT(?message) as ?totalMessageCount)
    WHERE {
      { ?message rdf:type snvoc:Comment } UNION { ?message rdf:type snvoc:Post } .
      ?message snvoc:creationDate ?creationDate .
      FILTER (?creationDate < "2017-07-15T11:50:56") .
      ?message snvoc:length ?length
    }
  }
  
  BIND ( ?messageCount / ?totalMessageCount as ?percentageOfMessages)
}
ORDER BY DESC(?messageYear) ?isComment ?lengthCategory
