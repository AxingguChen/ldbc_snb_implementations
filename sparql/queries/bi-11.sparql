PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX xsd: <http://www.w3.org/2001/XMLSchema#>
PREFIX sn: <http://www.ldbc.eu/ldbc_socialnet/1.0/data/>
PREFIX snvoc: <http://www.ldbc.eu/ldbc_socialnet/1.0/vocabulary/>
PREFIX sntag: <http://www.ldbc.eu/ldbc_socialnet/1.0/tag/>
PREFIX foaf: <http://xmlns.com/foaf/0.1/>
PREFIX dbpedia: <http://dbpedia.org/resource/>
PREFIX dbpedia-owl: <http://dbpedia.org/ontology/>

SELECT ?person ?tagName (COUNT(?like) AS ?countLikes) (COUNT(?reply) AS ?countReplies)
WHERE
{
    ?reply snvoc:content ?content  .
    ?reply snvoc:hasTag ?tag .
    ?tag foaf:name ?tagName
    FILTER( !REGEX(?content, $blacklist, "i") )
    ?reply snvoc:replyOf ?original .
    FILTER NOT EXISTS { ?original snvoc:hasTag ?tag }
    BIND ($country AS ?countryName)
    ?person a snvoc:Person .
    ?person snvoc:isLocatedIn/snvoc:isPartOf/foaf:name ?countryName .
    ?reply snvoc:hasCreator ?person .
    ?personWhoLiked a snvoc:Person .
    ?personWhoLiked snvoc:likes ?like .
    ?like snvoc:hasComment ?reply .
}
GROUP BY ?person ?tagName
ORDER BY DESC(?countLikes) ?person ?tagName
LIMIT 100

