PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX xsd: <http://www.w3.org/2001/XMLSchema#>
PREFIX sn: <http://www.ldbc.eu/ldbc_socialnet/1.0/data/>
PREFIX snvoc: <http://www.ldbc.eu/ldbc_socialnet/1.0/vocabulary/>
PREFIX sntag: <http://www.ldbc.eu/ldbc_socialnet/1.0/tag/>
PREFIX foaf: <http://xmlns.com/foaf/0.1/>
PREFIX dbpedia: <http://dbpedia.org/resource/>
PREFIX dbpedia-owl: <http://dbpedia.org/ontology/>

SELECT ?personId ?tagName (COUNT(?like) AS ?likeCount) (COUNT(?reply) AS ?replyCount)
WHERE
{
    ?reply snvoc:content ?content  .
    ?reply snvoc:hasTag ?tag .
    ?tag foaf:name ?tagName
    FILTER( !REGEX(?content, $blacklist, "i") )
    ?reply snvoc:replyOf ?original .
    FILTER NOT EXISTS { ?original snvoc:hasTag ?tag }
    BIND ($country AS ?countryName)
    ?personId a snvoc:Person .
    ?personId snvoc:isLocatedIn/snvoc:isPartOf/foaf:name ?countryName .
    ?reply snvoc:hasCreator ?personId .
    ?personIdWhoLiked a snvoc:Person .
    ?personIdWhoLiked snvoc:likes ?like .
    ?like snvoc:hasComment ?reply .
}
GROUP BY ?personId ?tagName
ORDER BY DESC(?likeCount) ?personId ?tagName
LIMIT 100
