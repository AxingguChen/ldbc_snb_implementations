PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX xsd: <http://www.w3.org/2001/XMLSchema#>
PREFIX sn: <http://www.ldbc.eu/ldbc_socialnet/1.0/data/>
PREFIX snvoc: <http://www.ldbc.eu/ldbc_socialnet/1.0/vocabulary/>
PREFIX sntag: <http://www.ldbc.eu/ldbc_socialnet/1.0/tag/>
PREFIX foaf: <http://xmlns.com/foaf/0.1/>
PREFIX dbpedia: <http://dbpedia.org/resource/>
PREFIX dbpedia-owl: <http://dbpedia.org/ontology/>

SELECT ?personId ?tagName (COUNT(?message) AS ?messageCount)
WHERE
{
    {
        SELECT DISTINCT ?personId ?message
        WHERE
        {
            ?rootPerson a snvoc:Person .
            ?rootPerson snvoc:id "$personId"^^xsd:long .

            # the next two lines are changed by the driver to UNION of 1-2-... knows "edges"
            # with the no-repeated-edge (a.k.a. edge isomorphic) matching semantics enforced
            $minPathDistance
            $maxPathDistance
            ?personId snvoc:isLocatedIn/snvoc:isPartOf/foaf:name $country .
            VALUES (?type) {(snvoc:Comment) (snvoc:Post)} .
            ?message a ?type .
            ?message snvoc:hasCreator ?personId .
            ?message snvoc:hasTag ?tagInClass .
            ?tagInClass a/rdfs:label $tagClass .
        }
    }
    ?message snvoc:hasTag/foaf:name ?tagName
}
GROUP BY ?personId ?tagName
ORDER BY DESC(?messageCount) ?tagName ?personId
LIMIT 100
