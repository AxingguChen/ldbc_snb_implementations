PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX xsd: <http://www.w3.org/2001/XMLSchema#>
PREFIX sn: <http://www.ldbc.eu/ldbc_socialnet/1.0/data/>
PREFIX snvoc: <http://www.ldbc.eu/ldbc_socialnet/1.0/vocabulary/>
PREFIX sntag: <http://www.ldbc.eu/ldbc_socialnet/1.0/tag/>
PREFIX foaf: <http://xmlns.com/foaf/0.1/>
PREFIX dbpedia: <http://dbpedia.org/resource/>
PREFIX dbpedia-owl: <http://dbpedia.org/ontology/>

SELECT DISTINCT (COUNT(*) AS ?count)
WHERE {
  ?country rdf:type dbpedia-owl:Country .
  ?country foaf:name "Spain" .
  ?a rdf:type snvoc:Person .
  ?b rdf:type snvoc:Person .
  ?c rdf:type snvoc:Person .

  # (a)-[:knows]-(b)
  {?a snvoc:knows ?ab . ?ab snvoc:hasPerson ?b }
  UNION
  {?b snvoc:knows ?ab . ?ab snvoc:hasPerson ?a }

  # (b)-[:knows]-(c)
  {?b snvoc:knows ?bc . ?bc snvoc:hasPerson ?c }
  UNION
  {?c snvoc:knows ?bc . ?bc snvoc:hasPerson ?b }

  # (c)-[:knows]-(a)
  {?c snvoc:knows ?ac . ?ac snvoc:hasPerson ?a }
  UNION
  {?a snvoc:knows ?ac . ?ac snvoc:hasPerson ?c }

  ?a snvoc:id ?aId .
  ?b snvoc:id ?bId .
  ?c snvoc:id ?cId .

  FILTER (?aId < ?bId && ?bId < ?cId)
}
GROUP BY ?country
