PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX xsd: <http://www.w3.org/2001/XMLSchema#>
PREFIX sn: <http://www.ldbc.eu/ldbc_socialnet/1.0/data/>
PREFIX snvoc: <http://www.ldbc.eu/ldbc_socialnet/1.0/vocabulary/>
PREFIX sntag: <http://www.ldbc.eu/ldbc_socialnet/1.0/tag/>
PREFIX foaf: <http://xmlns.com/foaf/0.1/>
PREFIX dbpedia: <http://dbpedia.org/resource/>
PREFIX dbpedia-owl: <http://dbpedia.org/ontology/>

SELECT ?messageCount (COUNT(?person) AS ?personCount)
WHERE
{ 
    {
        SELECT ?person (COUNT(?message) AS ?messageCount)
        WHERE
        {
            BIND (100 AS ?lengthTreshold)
            VALUES (?language) {$languages}
            
            {
                ?message a snvoc:Comment .
                ?message snvoc:replyOf+ ?post .
                ?message snvoc:creationDate ?creationDate .
                FILTER (?creationDate > $date) .
                ?post a snvoc:Post .
                ?post snvoc:language ?language .
            } UNION {
                ?message a snvoc:Post .
                ?message snvoc:language ?language .
            } .
            ?message snvoc:content ?content .
            FILTER(STRLEN(?content) > 0 && STRLEN(?content) < ?lengthTreshold)
            ?message snvoc:hasCreator ?person .
            ?person a snvoc:Person
        }
        GROUP BY ?person
    }
}
GROUP BY ?messageCount
ORDER BY DESC(?personCount) DESC(?messageCount)
