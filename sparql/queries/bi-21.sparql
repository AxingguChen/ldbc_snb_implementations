PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX xsd: <http://www.w3.org/2001/XMLSchema#>
PREFIX sn: <http://www.ldbc.eu/ldbc_socialnet/1.0/data/>
PREFIX snvoc: <http://www.ldbc.eu/ldbc_socialnet/1.0/vocabulary/>
PREFIX sntag: <http://www.ldbc.eu/ldbc_socialnet/1.0/tag/>
PREFIX foaf: <http://xmlns.com/foaf/0.1/>
PREFIX dbpedia: <http://dbpedia.org/resource/>
PREFIX dbpedia-owl: <http://dbpedia.org/ontology/>

SELECT ?zombieId (COUNT(?like) AS ?totalLikeCount) (SUM(?zombieLike) AS ?zombieLikeCount) (SUM(?zombieLike) / COUNT(?like) AS ?zombieScore)
WHERE
{   
    BIND($endDate AS ?endDate)
    {
        SELECT ?zombieId
        WHERE
        { 
            BIND($country AS ?contryName)
            BIND($endDate AS ?endDate)
            VALUES (?type) {(snvoc:Post) (snvoc:Comment)}
            ?message a ?type .
            ?message snvoc:creationDate ?messageCreationDate .
            FILTER( ?messageCreationDate < ?endDate )
            ?zombieId a snvoc:Person .
            ?zombieId snvoc:creationDate ?zombieCreationDate .
            FILTER( ?zombieCreationDate < ?endDate )
            ?message snvoc:hasCreator ?zombieId .
            ?zombieId snvoc:isLocatedIn/snvoc:isPartOf/foaf:name ?countryName 
            BIND( YEAR(?endDate) AS ?endYear)
            BIND( MONTH(?endDate) AS ?endMonth)
            BIND( YEAR(?zombieCreationDate) AS ?zombieCreationYear)
            BIND( MONTH(?zombieCreationDate) AS ?zombieCreationMonth)
            BIND( ((?endYear - ?zombieCreationYear) * 12 + ?endMonth - ?zombieCreationMonth) AS ?monthDiff)

        }
        GROUP BY ?zombieId ?monthDiff
        HAVING (COUNT(?message)/?monthDiff < 1.0)
    }
    VALUES (?type) {(snvoc:Post) (snvoc:Comment)}
    ?message a ?type .
    ?message snvoc:hasCreator ?zombieId .
    {        
        SELECT ?likerZombieId
        WHERE
        { 
            BIND($country AS ?contryName)
            BIND($endDate AS ?endDate)
            VALUES (?type) {(snvoc:Post) (snvoc:Comment)}
            ?message a ?type .
            ?message snvoc:creationDate ?messageCreationDate .
            FILTER( ?messageCreationDate < ?endDate )
            ?likerZombieId a snvoc:Person .
            ?likerZombieId snvoc:creationDate ?likerZombieCreationDate .
            FILTER( ?likerZombieCreationDate < ?endDate )
            ?message snvoc:hasCreator ?likerZombieId .
            ?likerZombieId snvoc:isLocatedIn/snvoc:isPartOf/foaf:name ?countryName 
            BIND( YEAR(?endDate) AS ?endYear)
            BIND( MONTH(?endDate) AS ?endMonth)
            BIND( YEAR(?likerZombieCreationDate) AS ?likerZombieCreationYear)
            BIND( MONTH(?likerZombieCreationDate) AS ?likerZombieCreationMonth)
            BIND( ((?endYear - ?likerZombieCreationYear) * 12 + ?endMonth - ?likerZombieCreationMonth) AS ?monthDiff)

        }
        GROUP BY ?likerZombieId ?monthDiff
        HAVING (COUNT(?message)/?monthDiff < 1.0)        
    }
    ?personId a snvoc:Person .
    ?personId snvoc:creationDate ?personCreationDate .
    ?personId snvoc:likes ?like .
    ?like snvoc:hasPost|snvoc:hasComment ?message .
    BIND ( IF(EXISTS{?likerZombieId snvoc:likes ?like}, 1 ,0) AS ?zombieLike)
}
GROUP BY ?zombieId
ORDER BY DESC(?zombieScore) ?zombieId 
LIMIT 100
